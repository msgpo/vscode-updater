# Maintainer: Vlad Zagorodniy <vladzzag@gmail.com>

pkgname=vscode-updater
pkgver=1.0
pkgrel=1
pkgdesc="Visual Studio Code updater"
arch=('x86_64')
url="https://github.com/zzag/vscode-updater"
license=('MIT')
makedepends=('git' 'go')
source=("$pkgname-$pkgver.tar.gz"::"https://github.com/zzag/vscode-updater/archive/$pkgver.tar.gz"
        'vscode-updater.service'
        'vscode-updater.sysusers')
sha256sums=('2e5fa8544f6f4782c394bc2bc13434e5a16ad082e30a6bbb3e8838ab2f3c6a4e'
            'b25561c2c675fcfe11bd07cf31fdd2eae8cb482fc0c1cc231ba7d9acf8a13bdc'
            '3b17562b1c71d95ef1849cd09d68616800bbea563ce7d827638f510053b462b4')

prepare() {
  cd "$pkgname-$pkgver"

  # go ignores vendor directory if the project is built outside of GOPATH
  mkdir src && cd src
  ln -s ../vendor/github.com
  ln -s ../vendor/golang.org
}

build() {
  cd "$pkgname-$pkgver"

  GOPATH=`pwd` go build
}

package() {
  cd "$pkgname-$pkgver"

  install -Dm755 "$pkgname-$pkgver" "$pkgdir/usr/bin/$pkgname"
  install -Dm644 "LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 "$srcdir/vscode-updater.service" "$pkgdir/usr/lib/systemd/system/vscode-updater.service"
  install -Dm644 "$srcdir/vscode-updater.sysusers" "$pkgdir/usr/lib/sysusers.d/vscode-updater.conf"
}

install=$pkgname.install

# vim:set ts=2 sw=2 et:
